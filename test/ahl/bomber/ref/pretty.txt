5 RANDOM : PRINT "YOU ARE NOW A PILOT IN A WORLD WAR II BOMBER"
10 INPUT "WHAT SIDE  ITALY(1), ALLIES(2). JAPANESE(3), GERMANY(4)"; A
20 IF A > 0 AND A < 5 THEN 25 ELSE PRINT "TRY AGAIN...." \ GOTO 10
25 ON A GOTO 30, 110, 200, 220
30 INPUT "WHAT IS YOUR TARGET  ALBANIA(1), GREECE(2), NORTH AFRICA(3) "; B
40 IF B > 0 AND B < 4 THEN 45 ELSE PRINT "TRY AGAIN...." \ GOTO 30
45 PRINT \ ON B GOTO 50, 80, 90
50 PRINT "SHOULD BE EASY. YOU'RE FLYING A NAZI-MADE PLANE,"
60 GOTO 280
80 PRINT "BE CAREFUL!!!" \ GOTO 280
90 PRINT "YOU'RE GOING FOR THE OIL, EH?" \ GOTO 280
110 INPUT "AIRCRAFT  LIBERATOR(1), B-29(2), B-17(3), LANCASTER(4)"; G
120 IF G > 0 AND G < 5 THEN 125 ELSE PRINT "TRY AGAIN...." \ GOTO 110
125 PRINT \ ON G GOTO 130, 150, 170, 190
130 PRINT "YOU'VE GOT 2 TONS OF BOMBS FLYING FOR PLOESTI"
140 GOTO 280
150 PRINT "YOU'RE DROPPING THE A-BOMB ON HIROSHIMA,"
160 GOTO 280
170 PRINT "YOU'RE CHASING THE BISMARK IN THE NORTH SEA,"
180 GOTO 280
190 PRINT "YOU'RE BUSTING A GERMAN HEAVY WATER PLANT IN THE RUHR,"
195 GOTO 280
200 PRINT "YOU'RE FLYING A KAMIKAZE (SUICIDE BOMBER) OVER THE USS LEXINGTON,"
205 INPUT "YOUR FIRST KAMIKAZE MISSION (Y OR N)"; F$
208 IF F$ = "N" THEN S = 0 \ GOTO 358
210 PRINT \ IF RND > .65 THEN 325 ELSE 380
220 PRINT "A NAZI, EH? OH WELL. ARE YOU GOING FOR RUSSIA(1), ENGLAND(2)"
230 INPUT "OR FRANCE(3)"; M \ IF M > 0 AND M < 4 THEN 235 ELSE PRINT "WHAT??" \ GOTO 220
235 PRINT \ ON M GOTO 250, 260, 270
250 PRINT "YOU'RE NEARING STALINGRAD" \ GOTO 280
260 PRINT "NEARING LONDON, BE CAREFUL, THEY'VE GOT A GOOD AIR-RAID DEFENCE,"
265 GOTO 280
270 PRINT "NEARING VERSAILLES. DUCK SOUP, THEY'RE NEARLY DEFENSELESS."
280 PRINT
285 INPUT "HOW MANY MISSIONS HAVE YOU FLOWN"; D
290 IF D < 160 THEN 300 ELSE PRINT "MISSIONS, NOT MILES...."
295 PRINT "150 MISSIONS IS HIGH EVEN FOR OLD-TIMERS, NOW THEN," \ GOTO 285
300 PRINT \ IF D < 100 THEN 310 ELSE PRINT "THAT'S PUSHING THE ODDS!" \ GOTO 320
310 IF D < 25 THEN PRINT "FRESH OUT OF TRAINING, EH?"
320 PRINT \ IF D < 160 * RND THEN 330
325 PRINT "DIRECT HIT!!!" T N T(100 * RND) "KILLED! MISSION SUCCESSFUL." \ GOTO 390
330 PRINT "MISSED TARGET BY" INT(2 + 30 * RND) "MILES!!"
335 PRINT "NOW YOU'RE REALLY IN FOR IT!!" \ PRINT
340 INPUT "DOES THE ENEMY HAVE GUNS(1), MISSILES(2), OR BOTH(3)"; R
345 IF R > 0 AND R < 4 THEN 350 ELSE PRINT "TRY AGAIN...." \ GOTO 340
350 PRINT \ IF R = 2 THEN 360
355 INPUT "WHAT IS THE PERCENT HIT RATE OF THE ENEMY GUNNERS (10 TO 50)"; S
358 IF S < 10 THEN PRINT "YOU LIE, BUT YOU'LL PAY...." \ PRINT \ GOTO 380
360 PRINT \ IF R > 1 THEN T = 35
365 IF S + T > 100 * RND THEN 380
370 PRINT "YOU MADE IT THROUGH TREMENDOUS FLAK!!" : GOTO 390
380 PRINT "***** BOOM *****"
384 PRINT "YOU HAVE BEEN SHOT DOWN"
386 PRINT "DEARLY BELOVED, WE ARE GATHERED HERE TODAY TO PAY OUR LAST TRIBUTE.."
390 PRINT \ PRINT \ PRINT \ INPUT "PLAY AGAIN (Y OR N)"; U$ \ IF U$ = "Y" THEN 10
400 PRINT "CHICKEN!!!!" \ PRINT \ PRINT
999 END

