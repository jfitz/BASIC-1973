10 REM GEOMEDIAVERAGE CALCULATION
11 OPTION REQUIRE_INITIALIZED TRUE
12 OPTION BASE 1
13 OPTION PRECISION 6
20 REM START WITH SOME NUMBERS
21 RANDOMIZE
22 ARR LET N0 = RND1(3, 100)
23 DIM N(3)
24 DIM M(3,100)
30 LET I = 1
40 GOSUB 500
100 REM PRINT THE NUMBERS
101 GOSUB 400
110 REM CALCULATE AVERAGE, GEOMETRIC MEAN, MEDIAN
111 LET N(1) = AVG(N0)
112 LET N(2) = PROD(N0)^(1/NELEM(N0))
113 LET N(3) = MEDIAN(N0)
120 REM CHECK FOR CONVERGENCE
121 IF NELEM(UNIQ1(N)) = 1 THEN 300
130 REM CHECK FOR STUCK LOOP
131 IF N(1) = N0(1) AND N(2) = N0(2) AND N(3) = N0(3) THEN 310
132 REM IF N() = N0() THEN 310
140 REM CHECK ITERATION LIMIT
141 IF I > 99 THEN 320
150 REM PERFORM ANOTHER LOOP
151 LET I = I + 1
152 ARR LET N0 = N
153 GOSUB 500
159 GOTO 100
300 REM RESULT - CONVERGENCE
301 GOSUB 410
302 PRINT "CONVERGENCE"
303 GOSUB 510
309 STOP
310 REM RESULT - STUCK IN LOOP
311 GOSUB 400
312 PRINT "STUCK IN LOOP"
313 GOSUB 510
319 STOP
320 REM RESULT - TOO MANY ITERATIONS
321 PRINT "TOO MANY ITERATIONS"
329 STOP
400 REM PROCEDURE - PRINT NUMBERS
401 PRINT I,
402 ARR PRINT N0
409 RETURN
410 REM PROCEDURE - PRINT NUMBERS
411 PRINT I,
412 ARR PRINT N
419 RETURN
500 REM STORE NUMBERS TO HISTORY
501 LET M(1,I) = N0(1)
502 LET M(2,I) = N0(2)
503 LET M(3,I) = N0(3)
509 RETURN
510 REM PLOT HISTORY
511 DIM M(3,I)
512 MAT PLOT M
999 END

