10 PRINT
20 PRINT
23 READ N
25 FOR S = N TO 1 STEP -1
30 READ P[S]
40 PRINT P[S];
50 NEXT S
55 PRINT
60 PRINT "INTEGRAL ZERO(S):  ";
68 REM TURN SWITCH OFF
70 LET K = 0
78 REM STUDY LINE 80 CAREFULLY!
80 FOR X = -P[1] TO P[1] STEP SGN(P[1])
88 REM LINE 90 PREVENTS AN ERROR MESSAGE CAUSED BY
89 REM DIVIDING BY ZERO
90 IF X = 0 THEN 140
98 REM IS X A FACTOR OF P[1]?
100 IF P[1] / X <> INT(P[1] / X) THEN 140
105 GOSUB 500
108 REM IS THE REMAINDER ZERO?
110 IF P <> 0 THEN 140
118 REM IF THE COMPUTER GETS THROUGH HERE, THE
119 REM VALUE OF X IS A ZERO OF THE FUNCTION
120 PRINT X;
128 REM TURN THE SWITCH ON - WE HAVE A ZERO
130 LET K = 1
140 NEXT X
150 IF K = 1 THEN 20
160 PRINT "NONE FOUND";
165 GOTO 20
170 DATA 4, 1, -2, -11, 12
180 DATA 4, 1, 1, -5, -2
190 DATA 4, 1, -2, 3, -4
200 DATA 5, 2, -1, -11, 11, -2
210 DATA 7, 2, -5, -6, 9, 9, -39, 36
490 REM SUBROUTINE 500 THROUGH 540 TAKES THE PLACE OF A
491 REM DEF STATEMENT AND EVALUATES A POLYNOMIAL OF
492 REM OF DEGREE N-1.
500 LET P = 0
510 FOR I = N TO 1 STEP -1
520 LET P = P + P[I] * X ^ (I - 1)
530 NEXT I
540 RETURN
999 END
