BASIC-1973 interpreter version -1

10 DEF FNP(X) = P[4] * X ^ 3 + P[3] * X ^ 2 + P[2] * X + P[1]
20 PRINT
22 PRINT
25 FOR S = 4 TO 1 STEP -1
30 READ P[S]
40 PRINT P[S];
50 NEXT S
60 PRINT "INTEGRAL ZERO(S):  ";
68 REM  TURN SWITCH OFF
70 LET K = 0
78 REM  STUDY LINE 80 CAREFULLY!
80 FOR X = -P[1] TO P[1] STEP SGN(P[1])
88 REM LINE 90 PREVENTS AN ERROR MESSAGE CAUSED BY
89 REM DIVIDING BY ZERO
90 IF X = 0 THEN 140
100 IF P[1] / X <> INT(P[1] / X) THEN 140
108 REM IS THE REMAINDER ZERO?
110 IF FNP(X) <> 0 THEN 140
118 REM IF THE COMPUTER GETS THROUGH HERE, THE
119 REM VALUE OF X IS A ZERO OF THE FUNCTION
120 PRINT X;
128 REM TURN THE SWITCH ON - WE HAVE A ZERO
130 LET K = 1
140 NEXT X
150 IF K = 1 THEN 20
160 PRINT "NONE FOUND";
165 GOTO 20
170 DATA 1, -2, -11, 12
180 DATA 1, 1, -5, -2
190 DATA 1, -2, 3, -4
200 DATA 2, -3, -10, 3
210 END

BASIC-1973 ended
