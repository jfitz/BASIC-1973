BASIC-1973 interpreter version -1

5 DIM P[10, 10]
10 READ N
15 IF N=0 THEN 999
20 FOR X = N TO 1 STEP -1
30 READ P[1,X]
40 PRINT P[1,X];
48 REM ENTER THE SAME COEFFICIENT IN THE SAME COLUMN
49 REM OF THE SECOND ROW
50 LET P[2,X] = P[1,X]
58 REM IF THE EXPONENT ON X IS ODD THEN CHANGE THE SIGN
60 IF (X+1)/2=INT((X+1)/2) THEN 80
70 LET P[2,X] = -P[2,X]
80 NEXT X
90 PRINT
100 PRINT N-1; "COMPLEX ZEROS"
200 LET V1,V2 = 0
210 FOR X = N-1 TO 1 STEP -1
218 REM LOOK AT POSITIVE ZEROS
220 IF P[1,X]*P[1,X+1]>0 THEN 240
230 LET V1 = V1+1
238 REM LOOK AT NEGATIVE ZEROS
240 IF P[2,X]*P[2,X+1]>0 THEN 260
250 LET V2 = V2+1
260 NEXT X
300 PRINT V1; "MAX POSITIVE"
310 PRINT V2; "MAX NEGATIVE"
320 PRINT
340 GOTO 10
500 DATA 4, 6, 103, 201, 90
510 DATA 5, 1, 3, 4, -8, 2
520 DATA 6, -4, -3, 5, -2, 1, -3
600 DATA 0
999 END

BASIC-1973 ended
