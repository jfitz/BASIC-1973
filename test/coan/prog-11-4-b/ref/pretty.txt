8 REM  INTZER BEGINS HERE  (WE HAVE REMOVED THE REM STATEMENTS)
9 REM  SEE THE PROGRAM FOR REM STATEMENTS
10 DEF FNP(X) = P[4] * X ^ 3 + P[3] * X ^ 2 + P[2] * X + P[1]
20 PRINT
30 PRINT
40 FOR S = 4 TO 1 STEP -1
50 READ P[S]
60 IF P[S] = .0101 THEN 430
70 PRINT P[S];
80 NEXT S
90 PRINT "INTEGRAL ZERO:"
100 FOR X = -P[1] TO P[1] STEP SGN(P[1])
110 IF X = 0 THEN 160
120 IF P[1] / X <> INT(P[1] / X) THEN 160
130 IF FNP(X) <> 0 THEN 160
140 PRINT X
150 GOTO 170
160 NEXT X
164 REM  INTZER ENDS HERE ***  SYNDIV BEGINS HERE
170 LET R = X
180 PRINT "SYNTHETIC DIVISION BY   X -"; R; "YIELDS:"
190 PRINT P[4];
200 FOR X = 3 TO 1 STEP -1
210 LET P[X] = P[X] + P[X + 1] * R
220 IF X > 1 THEN 240
230 PRINT "REMAINDER =";
240 PRINT P[X];
250 NEXT X
252 REM SYNDIV ENDS HERE
258 REM  HERE WE MOVE EACH ENTRY IN THE P LIST
259 REM   TO THE LOCATION ONE SUBSCRIPT LOWER
260 FOR X = 1 TO 3
270 LET P[X] = P[X + 1]
280 NEXT X
290 PRINT
298 REM COMP-1 BEGINS HERE
300 LET D = P[2] ^ 2 - 4 * P[3] * P[1]
310 LET F = 2 * P[3]
320 LET A = -P[2] / F
330 LET B = SQR(ABS(D)) / F
340 IF D < 0 THEN 380
350 PRINT "REAL ZEROS:"
360 PRINT A + B; "AND   "; A - B
370 GOTO 20
380 PRINT "NON-REAL ZEROS:"
390 PRINT "("; A; ","; B; ") AND ("; A; ","; -B; ")"
400 GOTO 20
405 DATA 1, 2, -1, -2
410 DATA 1, -1, 0, -48, 1, 0, 0, -1, 1, 0, 0, 1, 1, -1, 1, -1
415 DATA 6, -77, -189, -90
420 DATA .0101
430 END
 WARNING: Terminating statement in FOR/NEXT

