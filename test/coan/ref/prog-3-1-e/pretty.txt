BASIC-1973 interpreter version -1

10 REM **THIS PROGRAM IS INTENDED TO DEMONSTRATE SOME OF
20 REM **THE CAPABILITIES OF THE FOR-NEXT STATEMENT PAIR
30 REM
40 PRINT "50  FOR X=14 TO 20 PRODUCES THE FOLLIWING VALUES FOR X"
50 FOR X = 14 TO 20
60 PRINT X;
70 NEXT X
80 PRINT
90 REM  BASIC ALLOWS US TO INCREMENT A LOOP BY VALUES OTHER THAN ONE
100 PRINT
110 PRINT "120  FOR X=1 TO 19 STEP 2 PRODUCES:"
120 FOR X = 1 TO 19 STEP 2
130 PRINT X;
140 NEXT X
150 PRINT
160 REM    THE STEP NEED NOT INCREASE THE VALUE OF X
170 PRINT
180 PRINT "190  FOR X=345 TO 282 STEP -9 GIVES:"
190 FOR X = 345 TO 282 STEP -9
200 PRINT X;
210 NEXT X
220 PRINT
230 REM  DECIMALS ARE ALLOWED IN BASIC
240 PRINT
250 PRINT "260  FOR X=91.5 TO 3 STEP -15.7 YIELDS:"
260 FOR X = 91.5 TO 3 STEP -15.7
270 PRINT X;
280 NEXT X
290 PRINT
300 REM  VARIABLES MAY BE USED TO SET UP A MACHINE LOOP IN BASIC
310 PRINT
320 PRINT "330  LET A=5, 340  LET B=45,  350 LET C=6 AND"
325 PRINT "360  FOR V=A TO B STEP C  GIVES THESE RESULTS:"
330 LET A = 5
340 LET B = 45
350 LET C = 6
360 FOR V = A TO B STEP C
370 PRINT V;
380 NEXT V
390 END

BASIC-1973 ended
