BASIC-1973 interpreter version -1

8 REM   WE ARE READING FOUR ITEMS OF DATA WITH A LOOP
10 FOR S=1 TO 4
20 READ L[S]
30 NEXT S
38 PRINT "WE CAN PRINT THE ITEMS OF THE LIST EXPLICITLY"
40 PRINT "L[1]  L[2]  L[3]  L[4]"
50 PRINT L[1];L[2];L[3];L[4]
60 PRINT
70 PRINT "WE CAN ALSO USE A LOOP.  THE LONGER THE LIST THE BETTER"
80 PRINT "BEING ABLE TO USE A LOOP IS"
90 FOR X=1 TO 4
100 PRINT L[X];
110 NEXT X
130 PRINT
135 PRINT
140 PRINT "WE CAN OPERATE ON THE NUMBERS IN THE LIST"
145 PRINT "  B   L[B]  B*L[B]"
150 FOR B=1 TO 4
155 PRINT B;L[B];B*L[B]
160 NEXT B
170 PRINT
180 PRINT "WE CAN PRINT THE LIST BACKWARDS WITH FOR X=4 TO 1 STEP -1"
190 FOR X=4 TO 1 STEP -1
200 PRINT L[X];
210 NEXT X
220 PRINT
225 PRINT
230 PRINT "WE CAN RELOCATE ITEMS IN THE LIST"
240 PRINT "250 LET Z=L[1], 260 LET L[1]=L[2] AND 270 LET L[2]=Z"
245 PRINT "GIVE THE FOLLOWING RESULT"
250 LET Z=L[1]
252 REM **HERE WE ARE STORING THE VALUE OF L[1] IN Z
260 LET L[1]=L[2]
262 REM   WE HAVE PUT THE VALUE OF L[2] INTO L[1]
270 LET L[2]=Z
272 REM   HERE THE OLD VALUE OF L[1] IS PUT INTO L[2] FROM Z
280 PRINT "L[1] =";L[1]
290 PRINT "L[2] =";L[2]
300 PRINT "LOOK CAREFULLY AT THE ORIGINAL LIST"
310 PRINT "WE HAVE EXCHANGED ITEMS ONE AND TWO"
320 PRINT
330 PRINT "WE CAN PUSH EVERY NUMBER UP FOUR LOCATIONS IN THE LIST"
340 FOR P=1 TO 4
350 LET L[P+4]=L[P]
360 NEXT P
370 FOR N=1 TO 8
380 PRINT L[N];
390 NEXT N
500 DATA 4,9,-92,8
9999 END

BASIC-1973 ended
