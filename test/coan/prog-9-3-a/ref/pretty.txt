BASIC-1973 interpreter version -1

8 REM  WE COMPUTE THE CONVERSION FACTOR
10 LET R = 3.14159 / 180
18 REM   DEFINE THE TRIG FUNCTION FOR DEGREES
20 DEF FNT(G) = SIN(G * R)
28 REM   DEFINE A ROUNDING FUNCTION
30 DEF FNR(X) = INT(X * 100 + .5) / 100
38 REM   A1 AND B1 ARE ANGLES AND A IS A SIDE
40 READ A1, B1, A
50 IF A1 = 0 THEN 999
58 REM   FIND THE THIRD ANGLE
60 LET C1 = 180 - (A1 + B1)
68 REM   70 AND 80 COMPUTE THE OTHER TWO SIDES
70 LET B = A * FNT(B1) / FNT(A1)
80 LET C = B * FNT(C1) / FNT(B1)
88 REM COMPUTE AREA
90 LET A2 = .5 * A * B * FNT(C1)
98 REM NOW PRINT THE RESULTS
100 PRINT " ", " A", " B", " C"
110 PRINT "THE ANGLES ARE", A1, B1, C1
120 PRINT "THE SIDES ARE", FNR(A), FNR(B), FNR(C)
130 PRINT "AND THE AREA IS "; FNR(A2)
140 PRINT
150 GOTO 40
500 DATA 24, 51, 10
510 DATA 30, 60, 15
520 DATA 45, 45, 20
530 DATA 0, 0, 0
999 END

BASIC-1973 ended
